apply plugin: 'com.android.library'

ext {
    PUBLISH_GROUP_ID = 'com.furiousseraphim.chips'
    PUBLISH_ARTIFACT_ID = 'material-chips'
    PUBLISH_VERSION = '1.2.7'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 127
        versionName "1.2.7"
        setProperty("archivesBaseName", "material-chips-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'de.hdodenhof:circleimageview:2.0.0'
    compile('com.rengwuxian.materialedittext:library:2.1.4') {
        exclude module: 'appcompat-v7'
        exclude module: 'support-annotations'
    }
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'